buildscript {
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath("org.springframework.boot:spring-boot-gradle-plugin:1.5.7.RELEASE")
    }
}

plugins {
    id 'org.springframework.boot' version '1.5.7.RELEASE'
}

apply plugin: 'hoopoe-assemble-plugin'

sourceSets.create("itestApp")

configurations {
    itestAppCompile.exclude module: "spring-boot-starter-tomcat"
}

dependencies {
    compileOnly libraries.slf4j
    compileOnly project(':hoopoe-api')
    compileOnly libraries.lombok

    itestAppCompile "org.springframework.boot:spring-boot-starter-jdbc"
    itestAppCompile "org.springframework.boot:spring-boot-starter-web"
    itestAppCompile "org.springframework.boot:spring-boot-starter-jetty"
    itestAppCompile "mysql:mysql-connector-java:5.1.44"
    itestAppCompile "org.postgresql:postgresql:42.1.4"

    itestCompile libraries.commonTestDependencies
    itestCompile project(":hoopoe-integration-testing")
    itestCompile libraries.commonTestDependencies
    itestCompile libraries.lombok
}

hoopoeAssembly {
    pluginClass = 'hoopoe.plugins.SqlQueriesPlugin'
}

task itestAppJar(type: Jar) {
    classifier 'itest-app'
    from sourceSets.itestApp.output
}

bootRepackage {
    dependsOn itestAppJar
    withJarTask = itestAppJar
    customConfiguration = "itestAppCompile"
    // can be removed after https://github.com/spring-projects/spring-boot/issues/6846 is ready, v 2.0.0
    classifier "boot"
}

task copyItestApp(type: Copy) {
    from bootRepackage.outputs.files
    into sourceSets.itest.output.resourcesDir
    rename {
      "itest-app.jar"
    }
}

task copyPlugin(type: Copy) {
    dependsOn assembleHoopoeZip
    from assembleHoopoeZip.outputs.files
    into sourceSets.itest.output.resourcesDir
    rename {
      "sql-plugin.zip"
    }
}

itestClasses.dependsOn(copyItestApp, copyPlugin)

tasks["integration-tests"].onlyIf { System.getenv('ITEST_DB') != null }